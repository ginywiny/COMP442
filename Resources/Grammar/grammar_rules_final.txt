START -> PROG  . 

ADDOP -> plus  . 
ADDOP -> minus  . 
ADDOP -> or  . 

APARAMS -> EXPR REPTAPARAMS1  . 
APARAMS ->  . 

APARAMSTAIL -> comma EXPR  . 

ARITHEXPR -> TERM RIGHTRECARITHEXPR  . 

ARRAYSIZE -> lsqbr ARRAYSIZE1 .
ARRAYSIZE1 -> intnum rsqbr . 
ARRAYSIZE1 -> rsqbr  . 

ASSIGNOP -> equal  . 

EXPR -> ARITHEXPR  EXPR1 . 
EXPR1 ->  .
EXPR1 -> RELOP ARITHEXPR  .


FACTOR -> id FACTOR1 .
FACTOR1 -> REPTIDNEST1 FACTOR2 .
FACTOR1 -> lpar APARAMS rpar FACTOR2 .
FACTOR2 -> dot id FACTOR1 .
FACTOR2 -> .
FACTOR -> intlit  . 
FACTOR -> floatlit  . 
FACTOR -> lpar ARITHEXPR rpar  . 
FACTOR -> not FACTOR  . 
FACTOR -> SIGN FACTOR  . 


FPARAMS -> id colon TYPE REPTFPARAMS3 REPTFPARAMS4  . 
FPARAMS ->  . 

FPARAMSTAIL -> comma id colon TYPE REPTFPARAMSTAIL4  . 

FUNCBODY -> lcurbr REPTFUNCBODY1 rcurbr  . 

FUNCDECL -> FUNCHEAD semi  . 

FUNCDEF -> FUNCHEAD FUNCBODY  . 

FUNCHEAD -> func id lpar FPARAMS rpar arrow RETURNTYPE  . 

IMPLDEF -> impl id lcurbr REPTIMPLDEF3 rcurbr  . 

INDICE -> lsqbr ARITHEXPR rsqbr  . 

MEMBERDECL -> FUNCDECL  . 
MEMBERDECL -> VARDECL  . 

MULTOP -> mult  . 
MULTOP -> div  . 
MULTOP -> and  . 

OPTSTRUCTDECL2 -> inherits id REPTOPTSTRUCTDECL22  . 
OPTSTRUCTDECL2 ->  . 

PROG -> REPTPROG0  . 

RELEXPR -> ARITHEXPR RELOP ARITHEXPR  . 

RELOP -> eq  . 
RELOP -> neq  . 
RELOP -> lt  . 
RELOP -> gt  . 
RELOP -> leq  . 
RELOP -> geq  . 

REPTAPARAMS1 -> APARAMSTAIL REPTAPARAMS1  . 
REPTAPARAMS1 ->  . 

REPTFPARAMS3 -> ARRAYSIZE REPTFPARAMS3  . 
REPTFPARAMS3 ->  . 

REPTFPARAMS4 -> FPARAMSTAIL REPTFPARAMS4  . 
REPTFPARAMS4 ->  . 

REPTFPARAMSTAIL4 -> ARRAYSIZE REPTFPARAMSTAIL4  . 
REPTFPARAMSTAIL4 ->  . 

REPTFUNCBODY1 -> VARDECLORSTAT REPTFUNCBODY1  . 
REPTFUNCBODY1 ->  . 

REPTIDNEST1 -> INDICE REPTIDNEST1  . 
REPTIDNEST1 ->  . 

REPTIMPLDEF3 -> FUNCDEF REPTIMPLDEF3  . 
REPTIMPLDEF3 ->  . 

REPTOPTSTRUCTDECL22 -> comma id REPTOPTSTRUCTDECL22  . 
REPTOPTSTRUCTDECL22 ->  . 

REPTPROG0 -> STRUCTORIMPLORFUNC REPTPROG0  . 
REPTPROG0 ->  . 

REPTSTATBLOCK1 -> STATEMENT REPTSTATBLOCK1  . 
REPTSTATBLOCK1 ->  . 

REPTSTRUCTDECL4 -> VISIBILITY MEMBERDECL REPTSTRUCTDECL4  . 
REPTSTRUCTDECL4 ->  . 

REPTVARDECL4 -> ARRAYSIZE REPTVARDECL4  . 
REPTVARDECL4 ->  . 


RETURNTYPE -> TYPE  . 
RETURNTYPE -> void  . 

RIGHTRECARITHEXPR -> ADDOP TERM RIGHTRECARITHEXPR  . 
RIGHTRECARITHEXPR -> . 


RIGHTRECTERM ->  . 
RIGHTRECTERM -> MULTOP FACTOR RIGHTRECTERM  . 

SIGN -> plus  . 
SIGN -> minus  . 



STATBLOCK -> lcurbr REPTSTATBLOCK1 rcurbr  . 
STATBLOCK -> STATEMENT  . 
STATBLOCK ->  . 


STATEMENT -> id STATEMENT1 .
STATEMENT1 -> REPTIDNEST1 STATEMENT3 . 
STATEMENT1 -> lpar APARAMS rpar STATEMENT2 . 
STATEMENT2 -> dot id STATEMENT1.
STATEMENT2 -> semi .
STATEMENT3 -> dot id STATEMENT1.
STATEMENT3 -> ASSIGNOP EXPR semi . 
STATEMENT -> if lpar RELEXPR rpar then STATBLOCK else STATBLOCK semi  . 
STATEMENT -> while lpar RELEXPR rpar STATBLOCK semi  . 
STATEMENT -> read lpar VARIABLE rpar semi  . 
STATEMENT -> write lpar EXPR rpar semi  . 
STATEMENT -> return lpar EXPR rpar semi  . 


STRUCTDECL -> struct id OPTSTRUCTDECL2 lcurbr REPTSTRUCTDECL4 rcurbr semi  . 


STRUCTORIMPLORFUNC -> STRUCTDECL  . 
STRUCTORIMPLORFUNC -> IMPLDEF  . 
STRUCTORIMPLORFUNC -> FUNCDEF  . 

TERM -> FACTOR RIGHTRECTERM  . 

TYPE -> integer  . 
TYPE -> float  . 
TYPE -> id  . 

VARDECL -> let id colon TYPE REPTVARDECL4 semi  . 

VARDECLORSTAT -> VARDECL  . 
VARDECLORSTAT -> STATEMENT  . 


VARIABLE -> id VARIABLE1 .
VARIABLE1 -> REPTIDNEST1 VARIABLE3 .
VARIABLE1 -> lpar APARAMS rpar VARIABLE2 . 
VARIABLE2 -> dot id VARIABLE1 .
VARIABLE3 -> dot id VARIABLE1 .
VARIABLE3 ->  .


VISIBILITY -> public  . 
VISIBILITY -> private  . 


